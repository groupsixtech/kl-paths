PROGRAM test_layers
%NOBUSYLAMP
%COMMENT = 'test static layer class'
%NOPAUSESHFT
%NOPAUSE = ERROR + TPENABLE + COMMAND
%NOABORT = ERROR + COMMAND

%ifeq DEBUG_BUILD FALSE
%undef DEBUG_BUILD
%endif

%ifeq DEBUG_PLOT_POINTS FALSE
%undef DEBUG_PLOT_POINTS
%endif

%ifdef DEBUG_BUILD
%define SHOW_DEBUG FALSE
%define LOG_NAME 'RD:layer_debug.DT'
%define PLOT_NAME 'RD:path_plot.DT'
%include display.c.klt
%endif

%include systemvars.klt
%include pose.const.klt
%include pathlib.klt
%include toolpath.structs.klt
%include lam.klt

%include default_lam.klt

VAR
  layparams  : t_PATH_LAYER
  pthparams : t_PATH_PARAM
  mtnprm : t_MOTION_PRM
  toolprm : t_TOOL_VARS
  --work parameters
  lsrprm  IN CMOS : t_LASER
  powprm IN CMOS : t_POWDER
  overrideLAM IN CMOS : BOOLEAN

  -- set parameters
  file_name IN CMOS : STRING[20]
  run_type IN CMOS : INTEGER
  --
  pearch_rad IN CMOS : REAL
  pearch_dist IN CMOS : REAL

  start_offset IN CMOS : REAL
  stop_offset IN CMOS : REAL

  run_fast IN CMOS : BOOLEAN


%include display.klh
%from systemlib.klh %import VEC
%from pose.klh %import set_config
%from pathlib.klh %import new_coord_sys, new_layer_params, new_make_params, new_motion_params, new_tool_params
%include lamlib.klh

--layer object
%class tstlst('pathlayerstatic.klc','pathlayerstatic.klh','defaultstaticrotlayer.klt')

%ifdef DEBUG_BUILD
-- user display log
%class usrdis('dispclass.klc','dispclass.klh')
%endif
%ifdef DEBUG_PLOT_POINTS
-- plot debug
%class usrplt('dispclass.klc','dispclass.klh')
%endif

ROUTINE uninit_vars
  --file
%define TEST_FILE_NAME 'FR:DM14384-1.CSV'
%define LASER_POWER 4000
  --perch dist
%define DRAW_RADIUS 150
%define START_DIST 720
  --index
%define STARTPASS 0
%define STARTLAYER 0
  --powder
%defeval TEST_POWDER POWDER_INCONEL
          --change to MOVE_WORKING
%defeval PROG_TYPE MOVE_DRYRUN
  BEGIN
    IF UNINIT(file_name) THEN file_name = TEST_FILE_NAME ; ENDIF
    IF UNINIT(run_type) THEN run_type = PROG_TYPE ; ENDIF

    IF UNINIT(pearch_rad) THEN pearch_rad = DRAW_RADIUS ; ENDIF
    IF UNINIT(pearch_dist) THEN pearch_dist = START_DIST ; ENDIF
    IF UNINIT(start_offset) THEN start_offset = 0 ; ENDIF
    IF UNINIT(stop_offset) THEN stop_offset = 0 ; ENDIF

    IF UNINIT(run_fast) THEN run_fast = FALSE ; ENDIF

    IF UNINIT(lsrprm.power) THEN
      lsrprm = lamlib__new_laser_params(LASER_POWER)
    ENDIF
    IF UNINIT(powprm.powder) THEN 
      powprm.powder = TEST_POWDER
    ENDIF
    IF UNINIT(overrideLAM) THEN 
      overrideLAM = FALSE
    ENDIF
    IF UNINIT(layparams.start_pass) THEN
      layparams = paths__new_layer_params(STARTPASS, STARTLAYER, TRUE)
    ENDIF
  END uninit_vars

ROUTINE t_stat_pad
  VAR
  --offsets
%define HEIGHT_OFFSET 0
%define X_OFFSET 0
%define Y_OFFSET 0
%define TOOL_PITCH 0
  --motion params
%define RETRACT_HEIGHT 30
%define RETRACT_LENGTH 50
%define LINKING_SPEED 150
%define LINKING_STEP 20
    frm : XYZWPR
    origin : XYZWPR
    cds : t_COORD_SYS
  BEGIN
    frm = POS(0,0,0,0,0,0, pose__set_config(DEFAULT_CONFIG))
    cds = paths__new_coord_sys(PTH_CYLINDER, VERT_AXES)
    origin = POS(0,pearch_dist,pearch_rad,0,0,0,pose__set_config(DEFAULT_CONFIG))

    --initialize paths
      -- coord_frm, coord_sys (t_COORD_SYS), origin, idod, doPathPlan
    pthparams = paths__new_make_params(frm, cds, origin, 1, FALSE)
    --initialize motion
      -- program_type, interpolate, tool_speed, link_speed, useSetSpeed, tool_step, link_step, aprch_ofst, depart_ofst, inDegrees
    mtnprm = paths__new_motion_params(run_type, TRUE, 40, LINKING_SPEED, run_fast, 1, LINKING_STEP, start_offset, stop_offset, FALSE)
      -- retractDist, orient, tool pitch, leader pitch, idod, coord_system
    toolprm = paths__new_tool_params(VEC(0,RETRACT_LENGTH,RETRACT_HEIGHT), VEC(0,pearch_dist,pearch_rad), VEC(TOOL_ORIENT_DOWN), TOOL_PITCH, 0, 1, cds)

    --constructor
    tstlst__new(layparams, pthparams, mtnprm, toolprm, lsrprm, powprm, overrideLAM)

    --load layout & determine number of layers and passes
    tstlst__import_layout(file_name)

    --run loaded layout
    tstlst__run

    -- --cleanup memory
    -- tstlst__delete

  END t_stat_pad

BEGIN

%ifdef DEBUG_BUILD
  --(log_filename, show_date, show_debug, show_info)
  -- LOG_NAME
  usrdis__new('', FALSE, TRUE, TRUE)
  usrdis__clear_file
%endif
%ifdef DEBUG_PLOT_POINTS
  -- PLOT_NAME
  usrplt__new(PLOT_NAME, FALSE, TRUE, TRUE)
  usrplt__clear_file
%endif

-- uninit variables
uninit_vars

--run prog
t_stat_pad

%ifdef DEBUG_BUILD
  usrdis__write_pipe
  usrdis__delete
%endif

END test_layers