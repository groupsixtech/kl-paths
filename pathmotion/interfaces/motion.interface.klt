%ifndef pathclass_interface_t
%define pathclass_interface_t

-- ***********
-- Define type
-- ***********

-- for a simple type
%defeval MOTION_DATA_TYPE t_NODE
%defeval MOTION_DATA_JOINT_TYPE t_JNODE
%defeval MOTION_DATA_FILE rbtcfg

%mode push
%mode string QQQ "`" "`" "\\"
%define motion_type(parent) `TYPE
  t_NODE FROM parent = XYZWPR IN GROUP[RBT_GRP]
  t_JNODE FROM parent = JOINTPOS IN GROUP[RBT_GRP]`
%mode pop

--..warning:: this is undefined in default interface as
--            it may not be needed for the actual implemenatation.
--            Make sure it is redefined if needed
%undef motion_type

-- ***********
-- initialization  
-- ***********

%defeval DEFAULT_POSE new_pose(0,0,0,0,0,0, pose__set_config(DEFAULT_CONFIG))

-- ***********
-- Define interface header
-- ***********

%mode push
%mode string QQQ "$" "$" "\\"
%defeval impl_motion_head(parent) $
impl_motion_init_head(parent)
impl_motion_offsets_head(parent)
impl_motion_convert_head(parent)
impl_motion_setters_head(parent)
impl_motion_getters_head(parent)
impl_motion_operation_head(parent)
$
%mode pop


%mode push
%mode string QQQ "$" "$" "\\"

%define impl_motion_init_head(parent) $declare_member(parent,new_pose,parent,npose)
ROUTINE new_pose(x,y,z,w,p,r : REAL; cnfg : CONFIG) : MOTION_DATA_TYPE FROM parent
declare_member(parent,new_cart,parent,ncart)
ROUTINE new_cart(x,y,z,w,p,r : REAL; cnfg : CONFIG) : MOTION_DATA_TYPE FROM parent
declare_member(parent,new_joint,parent,njnt)
ROUTINE new_joint(j1,j2,j3,j4,j5,j6 : REAL) : MOTION_DATA_JOINT_TYPE FROM parent
$

%define impl_motion_offsets_head(parent) $declare_member(parent,make_tool_offset,parent,mktof)
ROUTINE make_tool_offset(ofst : XYZWPR) : MOTION_DATA_TYPE FROM parent
declare_member(parent,make_user_offset,parent,mkuof)
ROUTINE make_user_offset(toolprm : t_TOOL_VARS) : MOTION_DATA_TYPE FROM parent
declare_member(parent,define_tool_offset,parent,dftof)
ROUTINE define_tool_offset(toolprm : t_TOOL_VARS) : MOTION_DATA_TYPE FROM parent
$

%define impl_motion_convert_head(parent) $
declare_member(parent,tpath2pos,parent,tppse)
ROUTINE tpath2pos(nde : t_TOOLPATH) : MOTION_DATA_TYPE FROM parent
declare_member(parent,copy,parent,copy)
ROUTINE copy(nde1 : MOTION_DATA_TYPE; nde2 : MOTION_DATA_TYPE) FROM parent
declare_member(parent,copy_joint,parent,cpyj)
ROUTINE copy_joint(nde1 : MOTION_DATA_JOINT_TYPE; nde2 : MOTION_DATA_JOINT_TYPE) FROM parent
$

%define impl_motion_setters_head(parent) $declare_member(parent,set_lpos,parent,stlps)
ROUTINE set_lpos : MOTION_DATA_TYPE FROM parent
declare_member(parent,set_posereg,parent,stpr)
ROUTINE set_posereg(nde : MOTION_DATA_TYPE; reg_no : INTEGER) FROM parent
declare_member(parent,set_cart,parent,stcrt)
ROUTINE set_cart(p : XYZWPR; out_nde : MOTION_DATA_TYPE) FROM parent
declare_member(parent,set_component,parent,stcmp)
ROUTINE set_component(val : REAL; grp_no : INTEGER; comp_no : INTEGER; out_nde : MOTION_DATA_TYPE) FROM parent
$

%define impl_motion_getters_head(parent) $declare_member(parent,get_cart,parent,gtcrt)
ROUTINE get_cart(nde : MOTION_DATA_TYPE) : XYZWPR FROM parent
declare_member(parent,get_component,parent,gtcmp)
ROUTINE get_component(nde : MOTION_DATA_TYPE; grp_no : INTEGER; comp_no : INTEGER) : REAL FROM parent$

%define impl_motion_operation_head(parent) $declare_member(parent,poseMul,parent,psmul)
ROUTINE poseMul(nde1 : MOTION_DATA_TYPE; nde2 : MOTION_DATA_TYPE) : MOTION_DATA_TYPE FROM parent
declare_member(parent,poseInv,parent,psinv)
ROUTINE poseInv(nde1 : MOTION_DATA_TYPE) : MOTION_DATA_TYPE FROM parent$

%define impl_motion_paths_head(parent) $declare_member(parent,approachPath,parent,appth)
ROUTINE approachPath(speed : REAL; stepSize : REAL; coordSys : INTEGER; idod : INTEGER; origin : XYZWPR; offset : XYZWPR) FROM parent
declare_member(parent,retractPath,parent,rtpth)
ROUTINE retractPath(speed : REAL; stepSize : REAL; coordSys : INTEGER; idod : INTEGER; origin : XYZWPR; offset : XYZWPR) FROM parent
$

%mode pop

-- ***********
-- Define interface
-- ***********

%mode push
%mode string QQQ "$" "$" "\\"
%define impl_motion_members(parent) $
impl_motion_init_members(parent)
impl_motion_offsets_members(parent)
impl_motion_convert_members(parent)
impl_motion_setters_members(parent)
impl_motion_getters_members(parent)
impl_motion_operations_members(parent)
$
%mode pop


%mode push
%mode string QQQ "$" "$" "\\"

%define impl_motion_init_members(parent) $declare_member(parent,new_pose,parent,npose)
ROUTINE new_pose(x,y,z,w,p,r : REAL; cnfg : CONFIG) : MOTION_DATA_TYPE
  BEGIN
    RETURN( POS(x,y,z,w,p,r, cnfg) )
  END new_pose

declare_member(parent,new_cart,parent,ncart)
ROUTINE new_cart(x,y,z,w,p,r : REAL; cnfg : CONFIG) : MOTION_DATA_TYPE
  BEGIN
    RETURN( POS(x,y,z,w,p,r, cnfg) )
  END new_cart
declare_member(parent,new_joint,parent,njnt)
ROUTINE new_joint(j1,j2,j3,j4,j5,j6 : REAL) : MOTION_DATA_JOINT_TYPE
  VAR
    arr : ARRAY[6] OF REAL
  BEGIN
    arr[1] = j1; arr[2] = j2; arr[3] = j3; arr[4] = j4; arr[5] = j5; arr[6] = j6
    RETURN(pose__set_jointpos(arr))
  END new_joint
$

%define impl_motion_offsets_members(parent) $declare_member(parent,make_tool_offset,parent,mktof)
ROUTINE make_tool_offset(ofst : XYZWPR) : MOTION_DATA_TYPE
  VAR
    nde : MOTION_DATA_TYPE
  BEGIN
    nde = ofst
    RETURN( nde )
  END make_tool_offset

declare_member(parent,make_user_offset,parent,mkuof)
ROUTINE make_user_offset(toolprm : t_TOOL_VARS) : MOTION_DATA_TYPE
  BEGIN
    RETURN(new_pose(0,0,0,0,0,0, pose__set_config(DEFAULT_CONFIG)))
  END make_user_offset

%ifdef toolpath_structs_t
declare_member(parent,define_tool_offset,parent,dftof)
ROUTINE define_tool_offset(toolprm : t_TOOL_VARS) : MOTION_DATA_TYPE
  BEGIN
    --set tool offset
    RETURN( new_pose(0,0,0,(toolprm.orient.x + toolprm.pitch), toolprm.orient.y, toolprm.orient.z, pose__set_config(DEFAULT_CONFIG)) )

  END define_tool_offset
%endif
$

%define impl_motion_convert_members(parent) $
%ifdef pathlib_types_t
declare_member(parent,tpath2pos,parent,tppse)
ROUTINE tpath2pos(nde : t_TOOLPATH) : MOTION_DATA_TYPE
  VAR
    p : MOTION_DATA_TYPE
  BEGIN
    p = nde.v
    RETURN(p)
  END tpath2pos
%endif

declare_member(parent,copy,parent,copy)
ROUTINE copy(nde1 : MOTION_DATA_TYPE; nde2 : MOTION_DATA_TYPE)
  BEGIN
    nde2 = nde1
  END copy

declare_member(parent,copy_joint,parent,cpyj)
ROUTINE copy_joint(nde1 : MOTION_DATA_JOINT_TYPE; nde2 : MOTION_DATA_JOINT_TYPE)
  BEGIN
    nde2 = nde1
  END copy_joint
$

%define impl_motion_setters_members(parent) $declare_member(parent,set_lpos,parent,stlps)
ROUTINE set_lpos : MOTION_DATA_TYPE
  VAR
    nde : MOTION_DATA_TYPE
  BEGIN
    nde = pose__get_lpos(RBT_GRP)
    RETURN( nde )
  END set_lpos

declare_member(parent,set_posereg,parent,stpr)
ROUTINE set_posereg(nde : MOTION_DATA_TYPE; reg_no : INTEGER)
  BEGIN
    pose__set_posreg_xyz(nde, reg_no, RBT_GRP)
  END set_posereg

declare_member(parent,set_cart,parent,stcrt)
ROUTINE set_cart(p : XYZWPR; out_nde : MOTION_DATA_TYPE)
  BEGIN
    out_nde = p
  END set_cart

declare_member(parent,set_component,parent,stcmp)
ROUTINE set_component(val : REAL; grp_no : INTEGER; comp_no : INTEGER; out_nde : MOTION_DATA_TYPE)
  BEGIN
    SELECT comp_no OF
      CASE(1):
        out_nde.x = val
      CASE(2):
        out_nde.y = val
      CASE(3):
        out_nde.z = val
      CASE(4):
        out_nde.w = val
      CASE(5):
        out_nde.p = val
      CASE(6):
        out_nde.r = val
      ELSE:
        karelError(INVALID_TYPE_CODE, 'Component number is not valid', ER_ABORT)
    ENDSELECT

  END set_component

$

%define impl_motion_getters_members(parent) $declare_member(parent,get_cart,parent,gtcrt)
ROUTINE get_cart(nde : MOTION_DATA_TYPE) : XYZWPR
  BEGIN
    RETURN(nde)
  END get_cart

declare_member(parent,get_component,parent,gtcmp)
ROUTINE get_component(nde : MOTION_DATA_TYPE; grp_no : INTEGER; comp_no : INTEGER) : REAL
  BEGIN
    SELECT comp_no OF
      CASE(1):
        RETURN(nde.x)
      CASE(2):
        RETURN(nde.y)
      CASE(3):
        RETURN(nde.z)
      CASE(4):
        RETURN(nde.w)
      CASE(5):
        RETURN(nde.p)
      CASE(6):
        RETURN(nde.r)
      ELSE:
        karelError(INVALID_TYPE_CODE, 'Component number is not valid', ER_ABORT)
    ENDSELECT

    RETURN(0)
  END get_component
$

%define impl_motion_operations_members(parent) $declare_member(parent,poseMul,parent,psmul)
ROUTINE poseMul(nde1 : MOTION_DATA_TYPE; nde2 : MOTION_DATA_TYPE) : MOTION_DATA_TYPE
  BEGIN
    RETURN(nde1:nde2)
  END poseMul

declare_member(parent,poseInv,parent,psinv)
ROUTINE poseInv(nde1 : MOTION_DATA_TYPE) : MOTION_DATA_TYPE
  BEGIN
    RETURN(INV((nde1)))
  END poseInv
$

%define impl_motion_paths_members(parent) $declare_member(parent,approachPath,parent,appth)
ROUTINE approachPath(speed : REAL; stepSize : REAL; coordSys : INTEGER; idod : INTEGER; origin : XYZWPR; offset : XYZWPR)
  VAR
%define APPROACH_DEGREE_DIFF 5
    lpos : XYZWPR
    spos, spos2 : VECTOR
    vpos1, vpos2 : VECTOR
  BEGIN
    spos = VEC(origin.x,origin.y+offset.y,origin.z-idod*offset.z)
    spos2 = VEC(origin.x,origin.y,origin.z-idod*offset.z)

%ifdef DEBUG_BUILD
    usrdis__print(DEBUG, '* Approach *')
    usrdis__print(DEBUG, 'spos: ' + vec_to_s(spos, ','))
    usrdis__print(DEBUG, 'spos2: ' + vec_to_s(spos2, ','))
%endif

    --move to offset -> to zero poisition
    `PATH_OBJECT_NAME`__makeline(spos, spos2, (ZEROPOS(1)), PTH_LINKING, PTH_LINETO, stepSize, speed, 1, VEC(1,0,0))
    moveLine

    --origin point
    vpos1 = INV((origin)):spos2

    --get start position of path from drawing
    lpos = `PATH_OBJECT_NAME`__get_start_node
    vpos2 = VEC(lpos.x, lpos.y, lpos.z)

    --apply retract offset
    vpos2.z = vpos2.z-idod*offset.z

%ifdef DEBUG_BUILD
    usrdis__print(DEBUG, 'vpos1: ' + vec_to_s(vpos1, ','))
    usrdis__print(DEBUG, 'vpos2: ' + vec_to_s(vpos2, ','))
%endif

    IF (coordSys=PTH_CYLINDER) THEN
      --move between origin and start degree
      IF (ABS(vpos2.x - vpos1.x) > APPROACH_DEGREE_DIFF) THEN
        -- ..note:: set travel direction in step size
        `PATH_OBJECT_NAME`__makeline(vpos1, vpos2, origin, PTH_LINKING, PTH_LINETO, stepSize, speed, 1, VEC(1,0,0))
        moveArc
      ENDIF
    ENDIF

    IF (coordSys=PTH_CARTESAN) THEN
      `PATH_OBJECT_NAME`__makeline(vpos1, vpos2, origin, PTH_LINKING, PTH_LINETO, stepSize, speed, 1, VEC(1,0,0))
      moveLine
    ENDIF

  END approachPath
declare_member(parent,retractPath,parent,rtpth)
ROUTINE retractPath(speed : REAL; stepSize : REAL; coordSys : INTEGER; idod : INTEGER; origin : XYZWPR; offset : XYZWPR)
  VAR
%define APPROACH_DEGREE_DIFF 5
    lpos : XYZWPR
    lposOffset : REAL
    vpos1, vpos2, vpos3 : VECTOR
    spos : VECTOR
    fpos : VECTOR
  BEGIN

%ifdef DEBUG_BUILD
    usrdis__print(DEBUG, '* Retract *')
%endif

    lpos = pose__get_lpos(1)

    --if cylindrical. Convert.
    IF (coordSys=PTH_CYLINDER) THEN
      lpos = pose__cartesian_to_cylindrical((ZEROPOS(1)), lpos, VERT_AXES, 0, FALSE)
%ifdef DEBUG_BUILD
      usrdis__print(DEBUG, 'lpos: ' + vec_to_s(VEC(lpos.x, lpos.y, lpos.z), ','))
%endif
      --change degrees to mm
      lpos.x = ((360-lpos.x-0)*origin.z*M_PI/180)
    ENDIF

    -- set default orientation
    lpos = pose__replace_orient(lpos, paths__set_orientation(coordSys, idod))

    --convert into drawing frame
    lpos = INV((origin)):lpos
    --lpos.x = -1*idod*lpos.x
    vpos1 = VEC(lpos.x, lpos.y, lpos.z)

    -- retract point
    vpos2 = vpos1
    vpos2.z = vpos1.z-idod*offset.z

%ifdef DEBUG_BUILD
    usrdis__print(DEBUG, 'vpos1: ' + vec_to_s(vpos1, ','))
    usrdis__print(DEBUG, 'vpos2: ' + vec_to_s(vpos1, ','))
%endif

    --move up from lpos to the retract height
    `PATH_OBJECT_NAME`__makeline(vpos1, vpos2, origin, PTH_LINKING, PTH_LINETO, stepSize, speed, 1, VEC(1,0,0))
    moveLine

    -- get origin point
    spos = VEC(0,0,0)
    -- ..warning:: modifying z manually
    spos.z = vpos2.z

    --unravel if cylindrical above the retract distance (x,y,z) -> (theta,z,r)
    -- move back to start point in x
    IF (coordSys=PTH_CYLINDER) THEN
      --if same point. add 360 deg to lpos
      vpos3 = vpos2
      IF (ABS(spos.x - vpos3.x) < APPROACH_DEGREE_DIFF) THEN
        vpos3.x = vpos3.x - 360*origin.z*M_PI/180
      ENDIF

%ifdef DEBUG_BUILD
      usrdis__print(DEBUG, 'vpos3: ' + vec_to_s(vpos3, ','))
      usrdis__print(DEBUG, 'spos: ' + vec_to_s(spos, ','))
%endif

      -- ..note:: set travel direction in step size.
      `PATH_OBJECT_NAME`__makeline(vpos3, spos, origin, PTH_LINKING, PTH_LINETO, stepSize, speed, -1*idod, VEC(1,0,0))
      moveArc
    ENDIF

    spos = origin:spos
    fpos = VEC(origin.x,origin.y+offset.y,spos.z)

%ifdef DEBUG_BUILD
    usrdis__print(DEBUG, 'spos2: ' + vec_to_s(spos, ','))
    usrdis__print(DEBUG, 'fpos: ' + vec_to_s(fpos, ','))
%endif

    --move from the origin to the retract distance to the offset
    `PATH_OBJECT_NAME`__makeline(spos, fpos, (ZEROPOS(1)), PTH_LINKING, PTH_LINETO, stepSize, speed, 1, VEC(1,0,0))
    moveLine

  END retractPath
$

%mode pop

%endif -- pathclass_interface_t
