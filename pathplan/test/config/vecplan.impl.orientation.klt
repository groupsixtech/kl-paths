--include default interface
%include pathplan.interface.klt

-- ***********
-- Define interface header
-- ***********

%mode push
%mode string QQQ "`" "`" "\\"

%define define_pplan_headers(parent) `declare_member(parent,dist_to_weight,parent,d2wt)
ROUTINE dist_to_weight(dist : KDTREE_COMPARE_TYPE) : GRAPH_EDGE_VTYPE FROM parent
declare_member(parent,convert2pos,parent,cn2ps)
ROUTINE convert2pos(n : t_VEC_PATH ; ofst_orient : VECTOR; parent_frm : XYZWPR) : XYZWPR FROM parent
declare_member(parent,speed_blending,parent,spdbl)
ROUTINE speed_blending(start_speed : REAL ; end_speed : REAL; blendRatio : REAL; outNode : t_TOOLPATH) FROM parent
declare_member(parent,path_blending,parent,pthbl)
ROUTINE path_blending(p : PATH nodedata = t_TOOLPATH ; i : INTEGER; parent_frm : XYZWPR; blendRatio : REAL; outNode : t_TOOLPATH) FROM parent
declare_member(parent,convertOrientation,parent,cnvor)
ROUTINE convertOrientation(p : PATH nodedata = t_TOOLPATH ; frm : XYZWPR; radius : REAL; system : INTEGER; z_axis : INTEGER; flip : BOOLEAN) FROM parent
declare_member(parent,linkingTransition,parent,lktrs)
ROUTINE linkingTransition(p : PATH nodedata = t_TOOLPATH; spacing : REAL; idod : INTEGER) FROM parent
`
%mode pop

-- ***********
-- Define interface
-- ***********

%mode push
%mode string QQQ "`" "`" "\\"

%define define_pplan_members(parent) `declare_member(parent,dist_to_weight,parent,d2wt)
ROUTINE dist_to_weight(dist : KDTREE_COMPARE_TYPE) : GRAPH_EDGE_VTYPE
  BEGIN
    RETURN(TRUNC(dist))
  END dist_to_weight
declare_member(parent,convert2pos,parent,cn2ps)
ROUTINE convert2pos(n : t_VEC_PATH ; ofst_orient : VECTOR; parent_frm : XYZWPR) : XYZWPR
  VAR
    out : XYZWPR
    tangent : VECTOR
    sumOrient : VECTOR
  BEGIN
    
%ifdef DEBUG_BUILD
    usrdis__print(INFO, 'P' + i_to_s(pth_idx) + ': ' + '(' + vec_to_s(n.v, ',') + ')' + ',' + '(' + vec_to_s(n.tangent, ',') + ')')
    pth_idx = pth_idx + 1
%endif

    sumOrient = ofst_orient + (pose__vector_to_euler(n.tangent.x,n.tangent.y,n.tangent.z,X_AXES))

    out = pose__vector_to_pose(n.v, sumOrient, parent_frm.config_data)
    
    RETURN(out)
  END convert2pos
declare_member(parent,speed_blending,parent,spdbl)
ROUTINE speed_blending(start_speed : REAL ; end_speed : REAL; blendRatio : REAL; outNode : t_TOOLPATH)
  BEGIN
    outNode.speed = blendRatio*start_speed + (1-blendRatio)*end_speed
  END speed_blending
declare_member(parent,path_blending,parent,pthbl)
ROUTINE path_blending(p : PATH nodedata = t_TOOLPATH ; i : INTEGER; parent_frm : XYZWPR; blendRatio : REAL; outNode : t_TOOLPATH)
  VAR
    v1, v2 : VECTOR
    --o1, o2 : VECTOR
    vs, os : VECTOR
  BEGIN
    v1 = pose__pose_to_vector(p[i].v)
    v2 = pose__pose_to_vector(p[i+1].v)

    --o1 = pose__get_orientation(p[i].v)
    --o2 = pose__get_orientation(p[i+1].v)

    vs = blendRatio*v2 + (1-blendRatio)*v1
    os = pose__get_orientation(p[i].v)

    outNode.v = parent_frm:POS(vs.x, vs.y, vs.z, os.x, os.y, os.z, parent_frm.config_data)
    outNode.tangent = p[i].tangent

    speed_blending((p[i].speed), (p[i+1].speed), blendRatio, outNode)

  END path_blending
declare_member(parent,convertOrientation,parent,cnvor)
ROUTINE convertOrientation(p : PATH nodedata = t_TOOLPATH ; frm : XYZWPR; radius : REAL; system : INTEGER; z_axis : INTEGER; flip : BOOLEAN)
  VAR
    i : INTEGER
    surfpos : XYZWPR
  BEGIN
    --convert to cartesian
    SELECT (system) OF
      CASE(PTH_CYLINDER):
        FOR i=1 TO PATH_LEN(p) DO
          --convert surface arc into degrees
          surfpos = p[i].v
          surfpos.x = surfpos.x/radius*180/M_PI
          --convert coordinates
          p[i].v = pose__cylindrical_to_cartesian(frm, (surfpos), (z_axis))

          --convert tangent vector to Euler angles
          --replace orientation
          p[i].v = pose__replace_orient(p[i].v, pose__vector_to_euler_cylindrical(p[i].tangent, (-1*surfpos.x+180)))

          IF flip THEN
            p[i].v.w = p[i].v.w + 180 ;
          ENDIF
        ENDFOR
      CASE(PTH_POLAR):
        FOR i=1 TO PATH_LEN(p) DO
          --convert surface uv into theta and phi angles
          surfpos = p[i].v
          surfpos.x = surfpos.x/radius*180/M_PI
          surfpos.y = surfpos.y/radius*180/M_PI
          --convert coordinates
          p[i].v = pose__polar_to_cartesian(frm, (surfpos), (z_axis))

          --..warning::
          karelError(INVALID_TYPE_CODE,'convertOrientation: Fixed orientation not yet implemented for Spherical system', ER_ABORT)
        ENDFOR
      CASE(PTH_CARTESAN):
      ELSE:
    ENDSELECT
  END convertOrientation
declare_member(parent,linkingTransition,parent,lktrs)
ROUTINE linkingTransition(p : PATH nodedata = t_TOOLPATH; spacing : REAL; idod : INTEGER)
  BEGIN
    get_toolpath_interpolate(spacing, PTH_LINETO, TRUE, p)
  END linkingTransition
`
%mode pop