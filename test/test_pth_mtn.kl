PROGRAM test_pth_mtn
%NOBUSYLAMP
%COMMENT = 'test motion paths'
%NOPAUSE = COMMAND + TPENABLE + ERROR

%define TOOLING_STEP 6
%define LINKING_STEP 10
%define TOOLING_SPEED 50
%define LINKING_SPEED 100

--robot config parameters
%define USER_FRAME_CUT_NO 9
%define USER_FRAME_VERT_NO 7

%ifdef DEBUG_BUILD
%define SHOW_DEBUG FALSE
%define LOG_NAME 'RD:path_debug.DT'
%include display.c.klt
%endif

%include systemlib.klt
%include systemvars.klt
%include draw.klt
%include pathplan.klt
%include paths.klt

VAR
  contours : PATH nodedata = t_VEC_PATH
  lines : PATH nodedata = t_VEC_PATH
  polygon : PATH nodedata = t_VERTEX
  ppath : PATH nodedata = t_POS_PATH
  path_plan : PATH nodedata = t_INTEGER
  pth : PATH nodedata = t_TOOLPATH

%from systemlib.klh %import VEC, VEC2D
%include pose.klh
%from math.klh %import bitmask
%from strings.klh %import i_to_s, p_to_s, pose_to_s
%from registers.klh %import set_real, set_int
%include paths.klh

--path object
%class tstpth('paths.klc','pathclass.klh','defaultpath.klt')
--polygon object
%class tstpoly('polygon.klc', 'polygon.klh')

%ifdef DEBUG_BUILD
-- user display log
%class usrdis('dispclass.klc','dispclass.klh')
%endif

ROUTINE clear_poly(p : PATH nodedata = t_VERTEX)
  VAR
    i, status : INTEGER
  BEGIN
    i = PATH_LEN(p)
    WHILE PATH_LEN(p) > 0 DO
		  DELETE_NODE(p,i,status)
      i = i - 1
	  ENDWHILE
  END clear_poly

ROUTINE clear_list(p : PATH nodedata = t_INTEGER)
  VAR
    i, status : INTEGER
  BEGIN
    i = PATH_LEN(p)
    WHILE PATH_LEN(p) > 0 DO
		  DELETE_NODE(p,i,status)
      i = i - 1
	  ENDWHILE
  END clear_list

ROUTINE append_nodes(p : PATH nodedata = t_VERTEX; nodes : INTEGER)
  VAR
    i, status : INTEGER
  BEGIN
    FOR i=1 TO nodes DO
      APPEND_NODE(p,status)
    ENDFOR
  END append_nodes

ROUTINE draw_delta(rast_ang : REAL ; pitch : REAL; wall_lines : INTEGER)
  BEGIN
    --create polygon
    clear_poly(polygon)
    append_nodes(polygon, 8)   
    polygon[1].coords = VEC2D(-40,10)   
    polygon[2].coords = VEC2D(-10,40)   
    polygon[3].coords = VEC2D(0,0)
    polygon[4].coords = VEC2D(10,40)
    polygon[5].coords = VEC2D(40,10)
    polygon[6].coords = VEC2D(20,60)  
    polygon[7].coords = VEC2D(0,120)
    polygon[8].coords = VEC2D(-20,60)

    --initialize polygon object
    tstpoly__clear
    tstpoly__set_canvas((ZEROPOS(1)))
    tstpoly__append_polygon(polygon)

    --raster polygon
    tstpoly__set_params((rast_ang), 1, wall_lines, 0, pitch)
    tstpoly__raster(PTH_LINETO)

  END draw_delta

ROUTINE t_hexagon
  VAR
%define hex_radius 50
%define num_of_points 5
%define TOOL_ORIENT 20,0,0
%define RETRACT_HEIGHT 10
    i, status : INTEGER
    rast_ang : INTEGER
    overlap : REAL
    wall_lines : INTEGER
    bbox : t_RECT
    cds : t_COORD_SYS
    origin : XYZWPR
  BEGIN

    --lock group 1
    LOCK_GROUP(math__bitmask(RBT_GRP),status)

    --set frame
    pose__set_userframe(USER_FRAME_CUT_NO, RBT_GRP)
    pose__set_toolframe(TOOL_FRAME_NO, RBT_GRP)

    --draw hexagon
    rast_ang = 0
    overlap = 6
    wall_lines = 1
    origin = POS(0,0,0,0,0,0,pose__set_config(DEFAULT_CONFIG))

    tstpoly__new
    tstpoly__hexagon(hex_radius, num_of_points, VEC2D(hex_radius, hex_radius), 0)
    --get raster lines
    --     (start_angle, dir, wall_lines, line_width, line_pitch)
    tstpoly__set_params((rast_ang), 1, wall_lines, 0, overlap)
    tstpoly__raster(PTH_LINETO)
    
    paths__clear_vecpath(lines) ; paths__clear_vecpath(contours) ;
    tstpoly__lines_to_vec_path(lines) ; tstpoly__contours_to_vec_path(contours) ;

    cds = paths__new_coord_sys(PTH_CARTESAN, 3)
    tstpth__init(origin, POS(0,0,0,TOOL_ORIENT,pose__set_config(DEFAULT_CONFIG)), cds)

    tstpth__set_idod(1)

    --generate path
    bbox = tstpoly__get_bounding_box
    tstpth__createPath(lines, contours, origin, PLATE_CORNER, ONEWAY, FALSE)
    --move through path
    tstpth__set_prog_names(MOVE_DRYRUN)
    tstpth__movePolyFull(TOOLING_SPEED, TOOLING_STEP, LINKING_SPEED, LINKING_STEP, -1*(RETRACT_HEIGHT))

    --get lists
    tstpth__get_plan(path_plan)
    tstpth__get_path(pth)

    RETURN

  END t_hexagon

ROUTINE t_pvert_ID
  VAR

%define PAD_RADIUS 100
%define PAD_WIDTH 360
%define PAD_LENGTH 50
%defeval PAD_SEPERATION 0
%define PAD_START_DEG 0
%define NO_OF_PADS 1
%define RETRACT_RADIUS 40
%defeval RETRACT_HEIGHT (RETRACT_RADIUS-PAD_RADIUS)
%define PAD_IDOD -1

%define RASTER_ANGLE 0
%define PASS_OVERLAP 10
%define CONTOUR_LINES 0

%define TOOLING_SPEED 100
%define LINKING_SPEED 200
%define TOOLING_STEP 90
%define LINKING_STEP 90
    i, status : INTEGER
    origin : XYZWPR
    bbox : t_RECT
    cds : t_COORD_SYS
  BEGIN
    --lock group 1
    LOCK_GROUP(math__bitmask(RBT_GRP),status)

    --set frame
    pose__set_userframe(USER_FRAME_VERT_NO, RBT_GRP)
    pose__set_toolframe(TOOL_FRAME_NO, RBT_GRP)

    --draw pads
    tstpoly__new
    tstpoly__pads(PAD_WIDTH, PAD_LENGTH, PAD_SEPERATION, NO_OF_PADS, VEC2D(0,0))

    --get raster lines
    -- (start_angle, dir, wall_lines, line_width, line_pitch)
    tstpoly__set_params(RASTER_ANGLE, 1, CONTOUR_LINES, 0, PASS_OVERLAP)
    tstpoly__raster(PTH_LINETO)

    paths__clear_vecpath(lines) ; paths__clear_vecpath(contours) ;
    --get lines from object
    tstpoly__lines_to_vec_path(lines) ;

    cds = paths__new_coord_sys(PTH_CYLINDER, 3)
    tstpth__init(POS(0,0,0,0,0,0,pose__set_config(DEFAULT_CONFIG)), &
                 POS(0,0,0,TOOL_ORIENT_UP,pose__set_config(DEFAULT_CONFIG)), cds)
                
    tstpth__set_idod(PAD_IDOD)

    --generate path
    origin = POS(PAD_START_DEG,0,PAD_RADIUS,0,0,0,pose__set_config(DEFAULT_CONFIG))
    bbox = tstpoly__get_bounding_box
    tstpth__createPath(lines, contours, origin, ID_CORNER, ONEWAY, FALSE)
    --move through path
    tstpth__set_prog_names(MOVE_DRYRUN)
    tstpth__movePolyArcFull(TOOLING_SPEED, TOOLING_STEP, LINKING_SPEED, LINKING_STEP, RETRACT_HEIGHT)
    
    --get lists
    tstpth__get_plan(path_plan)
    tstpth__get_path(pth)

    -- retract
    tstpth__get_line(1, TRUE, TRUE, origin, PTH_LINKING, LINKING_STEP, FALSE)
    tstpth__moveArc(LINKING_SPEED)


    RETURN
  END t_pvert_ID


ROUTINE t_pvert_OD
  VAR

%define PAD_RADIUS 100
%define PAD_WIDTH 360
%define PAD_LENGTH 50
%defeval PAD_SEPERATION 0
%define PAD_START_DEG 130
%define NO_OF_PADS 1
%define RETRACT_RADIUS 30
%defeval RETRACT_HEIGHT (RETRACT_RADIUS)
%define PAD_IDOD 1

%define RASTER_ANGLE 0
%define PASS_OVERLAP 10
%define CONTOUR_LINES 0

%define TOOLING_SPEED 100
%define LINKING_SPEED 200
%define TOOLING_STEP 20
%define LINKING_STEP 20
    i, status : INTEGER
    origin : XYZWPR
    cds : t_COORD_SYS
  BEGIN
    --lock group 1
    LOCK_GROUP(math__bitmask(RBT_GRP),status)

    --set frame
    pose__set_userframe(USER_FRAME_VERT_NO, RBT_GRP)
    pose__set_toolframe(TOOL_FRAME_NO, RBT_GRP)

    --draw pads
    tstpoly__new
    tstpoly__pads(PAD_WIDTH, PAD_LENGTH, PAD_SEPERATION, NO_OF_PADS, VEC2D(0,0))

    --get raster lines
    -- (start_angle, dir, wall_lines, line_width, line_pitch)
    tstpoly__set_params(RASTER_ANGLE, 1, CONTOUR_LINES, 0, PASS_OVERLAP)
    tstpoly__raster(PTH_LINETO)

    paths__clear_vecpath(lines) ; paths__clear_vecpath(contours) ;
    --get lines from object
    tstpoly__lines_to_vec_path(lines) ;

    cds = paths__new_coord_sys(PTH_CYLINDER, 3)
    tstpth__init(POS(0,0,0,0,0,0,pose__set_config(DEFAULT_CONFIG)), &
                 POS(0,0,0,TOOL_ORIENT_DOWN,pose__set_config(DEFAULT_CONFIG)), cds)
                
    tstpth__set_idod(PAD_IDOD)

    --generate path
    origin = POS(PAD_START_DEG,0,PAD_RADIUS,0,0,0,pose__set_config(DEFAULT_CONFIG))
    tstpth__createPath(lines, contours, origin, OD_CORNER, ONEWAY, FALSE)
    --move through path
    tstpth__set_prog_names(MOVE_DRYRUN)
    tstpth__movePolyArcFull(TOOLING_SPEED, TOOLING_STEP, LINKING_SPEED, LINKING_STEP, -1*(RETRACT_HEIGHT))

    --get lists
    tstpth__get_plan(path_plan)
    tstpth__get_path(pth)


    -- retract
    tstpth__get_line(1, TRUE, TRUE, origin, PTH_LINKING, LINKING_STEP, FALSE)
    tstpth__moveArc(LINKING_SPEED)

    RETURN
  END t_pvert_OD

BEGIN
%ifdef DEBUG_BUILD
  --(log_filename, show_date, show_debug, show_info)
  usrdis__constructor(LOG_NAME, FALSE, TRUE, TRUE)
  usrdis__clear_file
%endif

  t_hexagon
  -- t_pvert_ID
  -- t_pvert_OD


  --clean up
  tstpoly__clear
  tstpth__destructor

%ifdef DEBUG_BUILD
  usrdis__write_pipe
  usrdis__destructor
%endif

END test_pth_mtn